<?xml version="1.0" encoding="UTF-8" standalone="no"?>
<event category="STEP" id="1">
  <actions>
    <action id="603" library="1">
      <!--action name: Code-->
      <kind>CODE</kind>
      <allowRelative>false</allowRelative>
      <question>false</question>
      <canApplyTo>true</canApplyTo>
      <actionType>CODE</actionType>
      <functionName/>
      <relative>false</relative>
      <not>false</not>
      <appliesTo>.self</appliesTo>
      <arguments>
        <argument kind="STRING">xoffset = view_xview[0];
yoffset = view_yview[0];
xsize = view_wview[0];
ysize = view_hview[0];
if(!taunting and !omnomnomnom)
{
    if(pressedKeys &amp; $01)  {
        if (!invisible &amp;&amp; cloakAlpha == 1)
            taunting=true;
        if (team==TEAM_RED) {
            tauntindex=0;
            tauntend=tauntlength-1;
        }
        else if (team==TEAM_BLUE) {
            tauntindex=tauntlength;
            tauntend=tauntlength*2-1;
        }
        image_speed=tauntspeed;
    } 
    
    if(pressedKeys &amp; $80){
       if(!place_free(x,y+1) and place_free(x,y)) {
           if(canCloak){
                if(currentWeapon.readyToStab){
                playsound(x,y,JumpSnd);
                vspeed = -jumpStrength;
                }
            }
            else {
                playsound(x,y,JumpSnd);
                vspeed = -jumpStrength;
            }
        }
        else if(canDoublejump and !doublejumpUsed and vspeed &gt; -jumpStrength) {
            vspeed = -jumpStrength;
            playsound(x,y,JumpSnd);
            doublejumpUsed = 1;
            moveStatus = 0;
        }
    }
       
    if(keyState &amp; $10)
        with(currentWeapon) event_user(1);
    else if(releasedKeys &amp; $10)
        with(currentWeapon) event_user(3);
    
}
// Right click
if(keyState &amp; $08 and (!taunting or player.class == CLASS_DEMOMAN) and !omnomnomnom)
    with(currentWeapon)
        event_user(2);
            
// Cloak
if ((pressedKeys &amp; $08) &amp;&amp; canCloak &amp;&amp; ((cloakAlpha &lt;= 0.5 and cloak) || (cloakAlpha == 1 and !cloak)) &amp;&amp; !intel  &amp;&amp; taunting==false &amp;&amp; !(keyState &amp; $10)) {
    cloak = !cloak;
}

switch(moveStatus) // moveStatus is reset in collision with obstacles (including doors if they reject you)
{
case 1: //If I am rocketing myself
    controlFactor = 0.65;
    frictionFactor = 1;
    break;
case 2: //If I am rocketing an enemy
    controlFactor = 0.3;
    frictionFactor = 1.05;
    break;
case 3: //Airblast
    controlFactor = 0.2;
    frictionFactor = 1;
    break;
case 4: //If I am rocketing a teamate/mining someone (more control than 2)
    controlFactor = 0.85;
    frictionFactor = 1;
    break;
default:
    if(!intel)
        controlFactor = baseControl;
    else
        controlFactor = baseControl-0.1;
    frictionFactor = baseFriction;
}

if(moveStatus == 1 or moveStatus == 2)
{
    if !variable_local_exists("jumpFlameParticleType")
    {
        jumpFlameParticleType = part_type_create();
        part_type_sprite(jumpFlameParticleType,FlameS,true,false,true);
        part_type_alpha2(jumpFlameParticleType,1,0.3);
        part_type_life(jumpFlameParticleType,2,5);
        part_type_scale(jumpFlameParticleType,0.7,-0.65);
    }
    vspeed -= 0.06;
    
    if !variable_global_exists("jumpFlameParticleSystem")
    {
        global.jumpFlameParticleSystem = part_system_create();
        part_system_depth(global.jumpFlameParticleSystem, 10);
    }
    
    if(global.particles == PARTICLES_NORMAL)
    {
        if(random(1) &gt; (controlFactor+frictionFactor)/2)
        {
            effect_create_below(ef_smoke,x-hspeed*1.2,y-vspeed*1.2+20,0,c_gray);
        }
    }
    if(global.particles == PARTICLES_NORMAL or global.particles == PARTICLES_ALTERNATIVE)
    {
        if(random(7) &lt; 5)
        {
            part_particles_create(global.jumpFlameParticleSystem,x,y+19,jumpFlameParticleType,1);
        }
    }
}
if(!taunting &amp;&amp; !omnomnomnom)
{
    if(keyState &amp; $40 and hspeed &gt; -runPower * controlFactor / (baseFriction - 1))
    {
        if (!canCloak)
            hspeed -= runPower*controlFactor;
        else if (currentWeapon.readyToStab)
            hspeed -= runPower*controlFactor;
    }
        
    if(keyState &amp; $20 and hspeed &lt; runPower * controlFactor / (baseFriction - 1))
    {   
        if (!canCloak)
            hspeed += runPower*controlFactor;
        else if (currentWeapon.readyToStab)
            hspeed += runPower*controlFactor;
    }
}
if(abs(hspeed) &gt; abs(1 * baseControl / (baseFriction - 1))*2)// divide friction as normal if going way too fast, based on normal max speed
    hspeed /= baseFriction;
else // otherwise divide by the moveStatus's friction
    hspeed /= frictionFactor;


if(pressedKeys &amp; $04) {
    setChatBubble(player, 45);
}
    
pressedKeys = 0;
releasedKeys = 0;


    
// flame bubble
if burnDuration&gt;0 &amp;&amp; player.class != CLASS_PYRO &amp;&amp; random(80) &lt;= 1 {
    setChatBubble(player, 49);
}

if(abs(hspeed)&lt;0.2 /*and not moveStatus = 3*/) {
    hspeed=0;
    animationImage=0;
}    
    
if(place_free(x,y+1)) {
    vspeed += 0.6;
    if(vspeed&gt;10) {
        vspeed=10;
    }
    animationImage = 1;
} else {
    if(place_free(x,y)) {
        doublejumpUsed = 0;
    }
}

if intel { 
    if random(1)&gt;0.90 &amp;&amp; speed&gt;2 {
        var sheet;
        sheet = instance_create(x,y-11+random(9),LooseSheet);
        sheet.hspeed = hspeed;
    }
    if cloak==true {
        cloak=false;
        cloakAplha=1;
    }
}
    
animationImage = (animationImage+abs(hspeed)/20) mod CHARACTER_ANIMATION_LEN;

if collision_point(x,y,SpawnRoom,0,0) player.quickspawn = 1;
else player.quickspawn=0;
    
// drop intel if ubered
if(intel) &amp;&amp; ubered {
    event_user(5);
}

//gotta regenerate some nuts
if nutsNBolts &lt; 100 nutsNBolts += 0.1;
else if nutsNBolts &gt; 100 nutsNBolts = 100;

//ubered max out ammo and extinguish flames
if ubered {
    if (burnIntensity &gt; 0 || burnDuration &gt; 0)
    {
        burnIntensity = 0;
        burnDuration = 0;
        burnedBy = -1;
    }
    if instance_exists(currentWeapon) {
        with(currentWeapon) {
            if(variable_local_exists("maxAmmo")) {
                ammoCount = maxAmmo;
            }
            alarm[5] = -1;
        }
    }
}

// Determine if the character is capturing a CP, and which
var zone;
zone = collision_point(x,y,CaptureZone,0,0);

if(zone&gt;=0 &amp;&amp; !cloak) {
    cappingPoint = zone.cp;
} else {
    cappingPoint = noone;
}
</argument>
      </arguments>
    </action>
  </actions>
</event>
